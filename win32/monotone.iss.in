; INNO Setup config file for monotone win32 setup
;
; all paths are related to "top_builddir"

#define BUILDDIR="."
#define APPNAME="monotone"
#define APPVERS="@VERSION@"
#define SRCDIR="@top_srcdir@"

[Setup]
AppName={#APPNAME}
AppVersion={#APPVERS}
AppVerName={#APPNAME} {#APPVERS}
OutputBaseFileName={#APPNAME}-{#APPVERS}-setup
AppCopyright=Copyright © 2002-2010 Graydon Hoare et al.
AppPublisher=The monotone Development Team
AppPublisherURL=http://www.monotone.ca/
DefaultDirName={pf}\{#APPNAME}
DefaultGroupName={#APPNAME}
MinVersion=4.0,4.0
OutputDir=.
AllowNoIcons=1
Compression=lzma/ultra
SolidCompression=yes
LicenseFile="{#SRCDIR}\COPYING"
ChangesEnvironment=true
WizardImageFile={#SRCDIR}\win32\monotone.bmp

[Files]
Source: "{#BUILDDIR}\mtn-stripped.exe"; DestDir: "{app}"; DestName: "mtn.exe"; Flags: ignoreversion
Source: "{#BUILDDIR}\html\*.html"; DestDir: "{app}\documentation\html"; Flags: ignoreversion
Source: "{#BUILDDIR}\html\*.css"; DestDir: "{app}\documentation\html"; Flags: ignoreversion
Source: "{#BUILDDIR}\html\figures\*.*"; DestDir: "{app}\documentation\html\figures"; Flags: ignoreversion
Source: "{#BUILDDIR}\html\images\*.*"; DestDir: "{app}\documentation\html\images"; Flags: ignoreversion
Source: "{#SRCDIR}\COPYING"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#SRCDIR}\monotone.info"; DestDir: "{app}\documentation"; Flags: ignoreversion
Source: "{#SRCDIR}\monotone.info-1"; DestDir: "{app}\documentation"; Flags: ignoreversion
Source: "{#SRCDIR}\monotone.info-2"; DestDir: "{app}\documentation"; Flags: ignoreversion
Source: "{#SRCDIR}\monotone.info-3"; DestDir: "{app}\documentation"; Flags: ignoreversion

; this file is generated by Makefile target "win32/dlls.iss"
; it creates list of distributable DLLs 
#include "dlls.iss"

; the same but for language files
#include "linguas.iss"

[Tasks]
Name: modifypath; Description: "Add monotone to your path"; GroupDescription: "Get up and running"; Flags: unchecked
Name: viewdocs; Description: "View the monotone documentation"; GroupDescription: "Get up and running"

[Run]
Filename: "{app}\documentation\html\index.html"; Tasks: viewdocs; Flags: shellexec nowait; WorkingDir: "{app}\documentation\html"

[Icons]
Name: "{group}\monotone documentation"; Filename: "{app}\documentation\html\index.html"; WorkingDir: "{app}\documentation\html"

[Code]
function ModPathDir(): String;
begin
  Result := ExpandConstant('{app}');
end;

#include "@top_srcdir@\win32\modpath.iss"

