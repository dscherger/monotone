1i\
[[tag migration-auto]]\
\

s/^=== (.*) ===/### \1/
s/^== (.*) ==/## \1/
s/^= (.*) =/# \1/
s/'''([^']+)'''/**\1**/g
s/''([^']+)''/*\1*/g
/^\{\{\{ *$/,/^\}\}\} *$/s/^/    /
/^    (\{\{\{|\}\}\}) *$/d
/^[^{]/s/\{\{\{ *([^}`]+) *\}\}\}/`\1`/g
/^[^{]/s/\{\{\{ *([^}]+) *\}\}\}/``\1``/g
s#\[(http://[^ ]+) ([^]]+)\]#[\2](\1)#g
s/\[\[BR\]\]$/<br>/
s/([^A-Za-z/\[\(\{])([A-Z]+[a-z]+[A-Z][A-Za-z0-9]+)/\1[[\2]]/g
s/([[:alpha:]])_([[:alpha:]])/\1\\_\2/g
/^ +\* /{
  x
  /^[^ ]/{
    x
    i\

    x
  }
  x
}
h
