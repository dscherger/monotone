#  -*- Autoconf -*-

AT_SETUP([netsync is not interrupted by SIGPIPE])
AT_KEYWORDS([netsync])

m4_define([NETSYNC_KILLPIPE], [
if test "$OSTYPE" == "msys"; then
  ps |awk -- '{p=$NF;a=1;if(p=="COMMAND")next;pp=split(p,ps,"/");if(ps[[pp]]=="monotone")system("kill -PIPE " $a);}'
else
  killall -PIPE monotone 2>/dev/null
fi
])

MONOTONE_SETUP
NETSYNC_SETUP

AT_DATA(testfile, [version 0 of test file
])
AT_CHECK(MONOTONE add testfile, [], [ignore], [ignore])
AT_CHECK(MONOTONE --branch=testbranch commit --message blah-blah, [], [ignore], [ignore])
F_VER0=`SHA1(testfile)`
VER0=`BASE_REVISION`

AT_DATA(testfile, [version 1 of test file
])
AT_CHECK(MONOTONE commit --message blah-blah, [], [ignore], [ignore])
F_VER1=`SHA1(testfile)`
VER1=`BASE_REVISION`

NETSYNC_SERVE_START(testbranch)

# send the server a SIGPIPE signal (it should survive)
NETSYNC_KILLPIPE

# this will fail if the SIGPIPE terminated it
NETSYNC_CLIENT_RUN(pull, testbranch)

NETSYNC_SERVE_STOP

AT_CLEANUP
